plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

configurations {
    shade
    implementation.extendsFrom shade
}

loom {
    accessWidenerPath = file("src/main/resources/clm.accesswidener")
}

repositories {

}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    /*implementation 'org.bytedeco:javacv-platform:1.5.8'
    implementation 'org.bytedeco:ffmpeg-platform:5.1.2-1.5.8'*/

    /*include(implementation('org.bytedeco:javacv-platform:1.5.8'))
    include(implementation('org.bytedeco:ffmpeg-platform:5.1.2-1.5.8'))*/

    shade 'org.bytedeco:javacv-platform:1.5.8'
    shade 'org.bytedeco:ffmpeg-platform:5.1.2-1.5.8'
}

configurations {
    all*.exclude group: 'org.bytedeco', module: 'opencv'
    all*.exclude group: 'org.bytedeco', module: 'flycapture'
    all*.exclude group: 'org.bytedeco', module: 'libdc1394'
    all*.exclude group: 'org.bytedeco', module: 'libfreenect2'
    all*.exclude group: 'org.bytedeco', module: 'videoinput'
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }

    withSourcesJar()
}

shadowJar {
    configurations = [project.configurations.shade]
    // Use a classifier to avoid overwriting the vanilla jar yet
    archiveClassifier.set('dev-shadow')
}

remapJar {
    dependsOn shadowJar
    inputFile.set(shadowJar.archiveFile)
    archiveClassifier.set('') // This ensures the final name is standard
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    repositories {

    }
}